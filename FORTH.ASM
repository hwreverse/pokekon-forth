 LD (IN+3),HL
 LD HL,(SAVESP)
 LD (SAVESP),SP
 LD SP,HL
 LD DE,ENTRY
 POP IX
NEXTBC:POP BC
NEXT:EX DE,HL
NEXTHL:LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 EX DE,HL
 JP (HL)
SAVESP:DW COLDSP
R0:DW ENTRY2
 DS 256
COLDSP:DW R0+2,0
S0:DW 0
DUP@:DB 3,'DUP'
DUP:PUSH BC
 JP NEXT
 DW DUP@
DROP@:DB 4,'DROP'
DROP:JP NEXTBC
 DW DROP@
SWAP@:DB 4,'SWAP'
SWAP:POP HL
 PUSH BC
 LD B,H
 LD C,L
 JP NEXT
 DW SWAP@
OVER@:DB 4,'OVER'
OVER:POP HL
 PUSH HL
 PUSH BC
 LD B,H
 LD C,L
 JP NEXT
 DW OVER@
ROT@:DB 3,'ROT'
ROT:POP HL
 EX (SP),HL
 PUSH BC
 LD B,H
 LD C,L
 JP NEXT
 DW ROT@
UNROT@:DB 5,'UNROT'
UNROT:LD H,B
 LD L,C
 POP BC
 EX (SP),HL
 PUSH HL
 JP NEXT
 DW UNROT@
NIP@:DB 3,'NIP'
NIP:POP HL
 JP NEXT
 DW NIP@
TUCK@:DB 4,'TUCK'
TUCK:POP HL
 PUSH BC
 PUSH HL
 JP NEXT
 DW TUCK@
PICK@:DB 4,'PICK'
PICK:LD H,B
 LD L,C
 ADD HL,HL
 ADD HL,SP
 LD C,(HL)
 INC HL
 LD B,(HL)
 JP NEXT
 DW PICK@
TWDUP@:DB 4,'2DUP'
TWDUP:CALL DOCOL
 DW OVER,OVER,EXIT,TWDUP@
TWDRP@:DB 5,'2DROP'
TWDRP:CALL DOCOL
 DW DROP,DROP,EXIT,TWDRP@
THDRP@:DB 5,'3DROP'
THDRP:CALL DOCOL
 DW DROP,DROP,DROP,EXIT,THDRP@
DEPTH@:DB 5,'DEPTH'
DEPTH:PUSH BC
 OR A
 LD HL,S0
 SBC HL,SP
 LD B,H
 LD C,L
 JP HALF
 DW DEPTH@
TOR@:DB 2,'>R'
TOR:LD (IX),C
 INC IX
 LD (IX),B
 INC IX
 JP NEXTBC
 DW TOR@
RFROM@:DB 2,'R>'
RFROM:PUSH BC
 DEC IX
 LD B,(IX)
 DEC IX
 LD C,(IX)
 JP NEXT
 DW RFROM@
RFET@:DB 2,'R@'
RFET:PUSH BC
 LD B,(IX-1)
 LD C,(IX-2)
 JP NEXT
 DW RFET@
STO@:DB 1,'!'
STO:LD H,B
 LD L,C
 POP BC
 LD (HL),C
 INC HL
 LD (HL),B
 JP NEXTBC
 DW STO@
FET@:DB 1,'@'
FET:LD H,B
 LD L,C
 LD C,(HL)
 INC HL
 LD B,(HL)
 JP NEXT
 DW FET@
PLSTO@:DB 2,'+!'
PLSTO:CALL DOCOL
 DW DUP,TOR,FET,ADD,RFROM,STO,EXIT,PLSTO@
INCST@:DB 3,'1+!'
INCST:LD H,B
 LD L,C
 INC (HL)
 JP NEXTBC
 DW INCST@
DECST@:DB 3,'1-!'
DECST:LD H,B
 LD L,C
 DEC (HL)
 JP NEXTBC
 DW DECST@
CSTO@:DB 2,'C!'
CSTO:LD H,B
 LD L,C
 POP BC
 LD (HL),C
 JP NEXTBC
 DW CSTO@
CFET@:DB 2,'C@'
CFET:LD A,(BC)
 LD C,A
 LD B,0
 JP NEXT
 DW CFET@
ADD@:DB 1,'+'
ADD:POP HL
 ADD HL,BC
 LD B,H
 LD C,L
 JP NEXT
 DW ADD@
SUB@:DB 1,'-'
SUB:POP HL
 OR A
 SBC HL,BC
 LD B,H
 LD C,L
 JP NEXT
 DW SUB@
AND@:DB 3,'AND'
AND:POP HL
 LD A,B
 AND H
 LD B,A
 LD A,C
 AND L
 LD C,A
 JP NEXT
 DW AND@
OR@:DB 2,'OR'
OR:POP HL
 LD A,B
 OR H
 LD B,A
 LD A,C
 OR L
 LD C,A
 JP NEXT
 DW OR@
XOR@:DB 3,'XOR'
XOR:POP HL
 LD A,B
 XOR H
 LD B,A
 LD A,C
 XOR L
 LD C,A
 JP NEXT
 DW XOR@
INV@:DB 6,'INVERT'
INV:LD A,B
 CPL
 LD B,A
 LD A,C
 CPL
 LD C,A
 JP NEXT
 DW INV@
NEG@:DB 6,'NEGATE'
NEG:LD A,B
 CPL
 LD B,A
 LD A,C
 CPL
 LD C,A
 INC BC
 JP NEXT
 DW NEG@
INC@:DB 2,'1+'
INC:INC BC
 JP NEXT
 DW INC@
DEC@:DB 2,'1-'
DEC:DEC BC
 JP NEXT
 DW DEC@
TWICE@:DB 2,'2*'
TWICE:SLA C
 RL B
 JP NEXT
 DW TWICE@
HALF@:DB 2,'2/'
HALF:SRA B
 RR C
 JP NEXT
 DW HALF@
UHALF@:DB 3,'U2/'
UHALF:SRL B
 RR C
 JP NEXT
 DW UHALF@
ZEQ@:DB 2,'0='
ZEQ:LD A,B
 OR C
 SUB 1
 SBC A,A
 LD B,A
 LD C,A
 JP NEXT
 DW ZEQ@
ZLT@:DB 2,'0<'
ZLT:SLA B
 SBC A,A
 LD B,A
 LD C,A
 JP NEXT
 DW ZLT@
EQ@:DB 1,'='
EQ:POP HL
 OR A
 SBC HL,BC
 JR Z,EQ1
 LD BC,0
 JP NEXT
EQ1:LD BC,-1
 JP NEXT
 DW EQ@
LT@:DB 1,'<'
LT:POP HL
 OR A
 SBC HL,BC
 JP PE,LT2
 JP P,LT3
LT1:LD BC,-1
 JP NEXT
LT2:JP M,LT3
 JR LT1
LT3:LD BC,0
 JP NEXT
 DW LT@
GT@:DB 1,'>'
GT:CALL DOCOL
 DW SWAP,LT,EXIT,GT@
ULT@:DB 2,'U<'
ULT:POP HL
 OR A
 SBC HL,BC
 SBC A,A
 LD B,A
 LD C,A
 JP NEXT
 DW ULT@
UGT@:DB 2,'U>'
UGT:CALL DOCOL
 DW SWAP,ULT,EXIT,UGT@
STOD@:DB 3,'S>D'
STOD:CALL DOCOL
 DW DUP,ZLT,EXIT,STOD@
ABS@:DB 3,'ABS'
ABS:CALL DOCOL
 DW DUP,ZLT,QBR,ABS1,NEG
ABS1:DW EXIT,ABS@
UMSTR@:DB 3,'UM*'
UMSTR:PUSH BC
 EXX
 POP BC
 POP DE
 LD HL,0
 LD A,17
 OR A
UMSTR1:RR H
 RR L
 RR D
 RR E
 JR NC,UMSTR2
 ADD HL,BC
UMSTR2:DEC A
 JR NZ,UMSTR1
 PUSH DE
 PUSH HL
 EXX
 JP NEXTBC
 DW UMSTR@
USTR@:DB 2,'U*'
USTR:CALL DOCOL
 DW UMSTR,DROP,EXIT,USTR@
UMSM@:DB 6,'UM/MOD'
UMSM:PUSH BC
 EXX
 POP BC
 POP HL
 POP DE
 LD A,16
 SLA E
 RL D
UMSM1:ADC HL,HL
 JR NC,UMSM2
 OR A
 SBC HL,BC
 OR A
 JR UMSM3
UMSM2:SBC HL,BC
 JR NC,UMSM3
 ADD HL,BC
 SCF
UMSM3:RL E
 RL D
 DEC A
 JR NZ,UMSM1
 PUSH HL
 PUSH DE
 EXX
 POP BC
 JP INV
 DW UMSM@
USM@:DB 5,'U/MOD'
USM:CALL DOCOL
 DW SWAP,STOD,ROT,UMSM,EXIT,USM@
EXIT@:DB 4,'EXIT'
EXIT:DEC IX
 LD D,(IX)
 DEC IX
 LD E,(IX)
 JP NEXT
 DW EXIT@
EXEC@:DB 7,'EXECUTE'
EXEC:LD H,B
 LD L,C
 POP BC
 JP (HL)
 DW EXEC@
TRUE@:DB 4,'TRUE'
TRUE:PUSH BC
 LD BC,-1
 JP NEXT
 DW TRUE@
FALSE@:DB 5,'FALSE'
FALSE:PUSH BC
 LD BC,0
 JP NEXT
 DW FALSE@
STATE@:DB 5,'STATE'
STATE:CALL DOCRE
 DW 0,STATE@
DP@:DB 2,'DP'
DP:CALL DOCRE
 DW ENDDCT,DP@
LATST@:DB 6,'LATEST'
LATST:CALL DOCRE
 DW LSTWRD,LATST@
HERE@:DB 4,'HERE'
HERE:CALL DOCOL
 DW DP,FET,EXIT,HERE@
ALLOT@:DB 5,'ALLOT'
ALLOT:CALL DOCOL
 DW DP,PLSTO,EXIT,ALLOT@
COM@:DB 1,','
COM:CALL DOCOL
 DW HERE,STO,LIT,2,ALLOT,EXIT,COM@
CCOM@:DB 2,'C,'
CCOM:CALL DOCOL
 DW HERE,CSTO,LIT,1,ALLOT,EXIT,CCOM@
COMCS@:DB 8,',COUNTED'
COMCS:CALL DOCOL
 DW DUP,CCOM,HERE,SWAP,DUP,ALLOT,CMOVE,EXIT,COMCS@
COUNT@:DB 5,'COUNT'
COUNT:CALL DOCOL
 DW DUP,INC,SWAP,CFET,EXIT,COUNT@
IN@:DB 2,'IN'
IN:CALL DOCRE
 DW 0,IN@
NIN@:DB 3,'#IN'
NIN:CALL DOCRE
 DW 0,NIN@
TOB@:DB 3,'TOB'
TOB:CALL DOCRE
TOBSZ EQU 256
 DS TOBSZ
 DW TOB@
OUT@:DB 3,'OUT'
OUT:CALL DOCRE
 DW 0,OUT@
EMIT@:DB 4,'EMIT'
EMIT:CALL DOCOL
 DW OUT,FET,CSTO,OUT,INCST,EXIT,EMIT@
CR@:DB 2,'CR'
CR:CALL DOCOL
 DW LIT,0AH,EMIT,EXIT,CR@
SPACE@:DB 5,'SPACE'
SPACE:CALL DOCOL
 DW LIT,' ',EMIT,EXIT,SPACE@
ONESS@:DB 8,'1/STRING'
ONESS:CALL DOCOL
 DW SWAP,INC,SWAP,DEC,EXIT,ONESS@
TYPE@:DB 4,'TYPE'
TYPE:CALL DOCOL
TYPE1:DW DUP,QBR,TYPE2,OVER,CFET,EMIT,ONESS,BR,TYPE1
TYPE2:DW TWDRP,EXIT,TYPE@
PARSE@:DB 5,'PARSE'
PARSE:CALL DOCOL
 DW TOR,IN,FET,NIN,FET
PARSE1:DW DUP,QBR,PARSE2,ONESS,OVER,DEC,CFET,RFET,EQ,QBR,PARSE1
PARSE2:DW RFROM,DROP,NIN,STO,IN,FET,SWAP,DUP,IN,STO,OVER,SUB,DEC,EXIT,PARSE@
PAREN@:DB 1+80H,'('
PAREN:CALL DOCOL
 DW LIT,')',PARSE,TWDRP,EXIT,PAREN@
COMQ@:DB 2+80H,',"'
COMQ:CALL DOCOL
 DW LIT,'"',PARSE,COMCS,EXIT,COMQ@
XSQUO@:DB 4,'(S")'
XSQUO:CALL DOCOL
 DW RFROM,COUNT,TWDUP,ADD,TOR,EXIT,XSQUO@
SQUO@:DB 2+80H,'S"'
SQUO:CALL DOCOL
 DW LIT,XSQUO,COM,COMQ,EXIT,SQUO@
DQUO@:DB 2+80H,'."'
DQUO:CALL DOCOL
 DW SQUO,LIT,TYPE,COM,EXIT,DQUO@
XTERM@:DB 10,'(TERMINAL)'
XTERM:PUSH BC
 PUSH IX
 LD HL,(SAVESP)
 LD (SAVESP),SP
 LD SP,HL
 LD HL,TOB+3
 RET
 DW XTERM@
TERM@:DB 8,'TERMINAL'
TERM:CALL DOCOL
 DW LIT,0,EMIT,XTERM
ENTRY:DW LIT,-1,NIN,STO,LIT,0,PARSE,NIN,STO,IN,STO,TOB,OUT,STO,EXIT,TERM@
TBR@:DB 7,'''BRANCH'
TBR:CALL DOCRE
BR:EX DE,HL
 LD E,(HL)
 INC HL
 LD D,(HL)
 INC HL
 JP NEXT
 DW TBR@
TQBR@:DB 8,'''?BRANCH'
TQBR:CALL DOCRE
QBR:LD A,B
 OR C
 POP BC
 JR Z,BR
 INC DE
 INC DE
 JP NEXT
 DW TQBR@
IF@:DB 2+80H,'IF'
IF:CALL DOCOL
 DW TQBR,COM,HERE,DUP,COM,EXIT,IF@
THEN@:DB 4+80H,'THEN'
THEN:CALL DOCOL
 DW HERE,SWAP,STO,EXIT,THEN@
ELSE@:DB 4+80H,'ELSE'
ELSE:CALL DOCOL
 DW TBR,COM,HERE,DUP,COM,EXIT,ELSE@
BEGIN@:DB 5+80H,'BEGIN'
BEGIN:JP HERE
 DW BEGIN@
UNTIL@:DB 5+80H,'UNTIL'
UNTIL:CALL DOCOL
 DW TQBR,COM,COM,EXIT,UNTIL@
AGAIN@:DB 5+80H,'AGAIN'
AGAIN:CALL DOCOL
 DW TBR,COM,COM,EXIT,AGAIN@
WHILE@:DB 5+80H,'WHILE'
WHILE:JP IF
 DW WHILE@
REP@:DB 6+80H,'REPEAT'
REP:CALL DOCOL
 DW SWAP,AGAIN,THEN,EXIT,REP@
BASE@:DB 4,'BASE'
BASE:CALL DOCRE
 DW 10,BASE@
HP@:DB 2,'HP'
HP:CALL DOCRE
 DW 0
 DS 34
H0:DW HP@
LTNUM@:DB 3,'<#'
LTNUM:CALL DOCOL
 DW LIT,H0,HP,STO,EXIT,LTNUM@
HOLD@:DB 4,'HOLD'
HOLD:CALL DOCOL
 DW HP,DECST,HP,FET,CSTO,EXIT,HOLD@
NUM@:DB 1,'#'
NUM:CALL DOCOL
 DW BASE,FET,USM,SWAP,DUP,LIT,9,GT,LIT,7,AND,ADD,LIT,30H,ADD,HOLD,EXIT,NUM@
NUMS@:DB 2,'#S'
NUMS:CALL DOCOL
NUMS1:DW NUM,DUP,ZEQ,QBR,NUMS1,EXIT,NUMS@
SIGN@:DB 4,'SIGN'
SIGN:CALL DOCOL
 DW ZLT,QBR,SIGN1,LIT,'-',HOLD
SIGN1:DW EXIT,SIGN@
NUMGT@:DB 2,'#>'
NUMGT:CALL DOCOL
 DW DROP,HP,FET,LIT,H0,OVER,SUB,EXIT,NUMGT@
DOT@:DB 1,'.'
DOT:CALL DOCOL
 DW LTNUM,DUP,ABS,NUMS,SWAP,SIGN,NUMGT,TYPE,SPACE,EXIT,DOT@
DOTS@:DB 2,'.S'
DOTS:CALL DOCOL
 DW DEPTH
DOTS1:DW DUP,QBR,DOTS2,DUP,PICK,DOT,DEC,BR,DOTS1
DOTS2:DW DROP,EXIT,DOTS@
GRAQ@:DB 8,'GRAPHIC?'
GRAQ:CALL DOCOL
 DW LIT,' ',UGT,EXIT,GRAQ@
SKIP@:DB 4,'SKIP'
SKIP:CALL DOCOL
SKIP1:DW DUP,QBR,SKIP3,OVER,CFET,GRAQ,QBR,SKIP2,EXIT
SKIP2:DW ONESS,BR,SKIP1
SKIP3:DW EXIT,SKIP@
SCAN@:DB 4,'SCAN'
SCAN:CALL DOCOL
SCAN1:DW DUP,QBR,SCAN3,OVER,CFET,GRAQ,ZEQ,QBR,SCAN2,EXIT
SCAN2:DW ONESS,BR,SCAN1
SCAN3:DW EXIT,SCAN@
WORD@:DB 4,'WORD'
WORD:CALL DOCOL
 DW IN,FET,NIN,FET,SKIP,OVER,TOR,SCAN,OVER,TOR,DUP,QBR,WORD1,ONESS
WORD1:DW NIN,STO,IN,STO,RFROM,RFET,SUB,RFROM,SWAP,EXIT,WORD@
CNFA@:DB 9,'COUNT-NFA'
CNFA:CALL DOCOL
 DW COUNT,LIT,7FH,AND,EXIT,CNFA@
IMMQ@:DB 6,'IMMED'
IMMQ:CALL DOCOL
 DW CFET,LIT,80H,AND,EXIT,IMMQ@
NTOL@:DB 7,'NFA>LFA'
NTOL:CALL DOCOL
 DW DEC,DEC,EXIT,NTOL@
NTOC@:DB 7,'NFA>CFA'
NTOC:CALL DOCOL
 DW CNFA,ADD,EXIT,NTOC@
SEQ@:DB 2,'S='
SEQ:CALL DOCOL
 DW ROT,OVER,EQ,ZEQ,QBR,SEQ1,THDRP,FALSE,EXIT
SEQ1:DW DUP,QBR,SEQ3,TOR,OVER,CFET,OVER,CFET,EQ,ZEQ,QBR,SEQ2,RFROM,THDRP,FALSE,EXIT
SEQ2:DW SWAP,INC,SWAP,INC,RFROM,DEC,BR,SEQ1
SEQ3:DW THDRP,TRUE,EXIT,SEQ@
FIND@:DB 4,'FIND'
FIND:CALL DOCOL
 DW LATST,FET,TOR
FIND1:DW TWDUP,RFET,CNFA,SEQ,ZEQ,QBR,FIND3,RFROM,NTOL,FET,DUP,ZEQ,QBR,FIND2,THDRP,FALSE,EXIT
FIND2:DW TOR,BR,FIND1
FIND3:DW TWDRP,RFET,NTOC,RFROM,IMMQ,ZEQ,LIT,1,OR,EXIT,FIND@
TXLIT@:DB 10,'''(LITERAL)'
TXLIT:CALL DOCRE
LIT:PUSH BC
 EX DE,HL
 LD C,(HL)
 INC HL
 LD B,(HL)
 INC HL
 JP NEXTHL
 DW TXLIT@
LITRL@:DB 7,'LITERAL'
LITRL:CALL DOCOL
 DW STATE,FET,QBR,LITRL1,TXLIT,COM,COM
LITRL1:DW EXIT,LITRL@
DIGQ@:DB 6,'DIGIT?'
DIGQ:CALL DOCOL
 DW DUP,LIT,39H,GT,LIT,100H,AND,ADD,DUP,LIT,140H,GT,LIT,107H,AND,SUB,LIT,30H,SUB,DUP,BASE,FET,ULT,EXIT,DIGQ@
SIGNQ@:DB 5,'SIGN?'
SIGNQ:CALL DOCOL
 DW OVER,CFET,LIT,2DH,EQ,DUP,QBR,SIGNQ1,TOR,ONESS,RFROM
SIGNQ1:DW EXIT,SIGNQ@
TONUM@:DB 7,'>NUMBER'
TONUM:CALL DOCOL
TONUM1:DW DUP,QBR,TONUM3,OVER,CFET,DIGQ,ZEQ,QBR,TONUM2,DROP,EXIT
TONUM2:DW TOR,ROT,BASE,FET,USTR,RFROM,ADD,UNROT,ONESS,BR,TONUM1
TONUM3:DW EXIT,TONUM@
NUMQ@:DB 7,'NUMBER?'
NUMQ:CALL DOCOL
 DW LIT,0,UNROT,SIGNQ,TOR,TONUM,NIP,QBR,NUMQ1,DROP,RFROM,DROP,FALSE,EXIT
NUMQ1:DW RFROM,QBR,NUMQ2,NEG
NUMQ2:DW TRUE,EXIT,NUMQ@
INTER@:DB 9,'INTERPRET'
INTER:CALL DOCOL
INTER1:DW WORD,DUP,QBR,INTER5,TWDUP,FIND,DUP,QBR,INTER3,ROT,DROP,ROT,DROP,INC,STATE,FET,ZEQ,OR,QBR,INTER2,EXEC,BR,INTER1
INTER2:DW COM,BR,INTER1
INTER3:DW DROP,TWDUP,NUMQ,QBR,INTER4,NIP,NIP,LITRL,BR,INTER1
INTER4:DW TYPE,LIT,'?',EMIT,ABORT
INTER5:DW TWDRP,EXIT,INTER@
XQUIT@:DB 6,'(QUIT)'
XQUIT:LD IX,R0
 JP NEXT
 DW XQUIT@
QUIT@:DB 4,'QUIT'
QUIT:CALL DOCOL
 DW XQUIT,FALSE,STATE,STO
QUIT1:DW TERM
ENTRY2:DW INTER,BR,QUIT1,QUIT@
EMPTY@:DB 5,'EMPTY'
EMPTY:LD SP,S0
 JP NEXT
 DW EMPTY@
ABORT@:DB 5,'ABORT'
ABORT:CALL DOCOL
 DW EMPTY,QUIT,ABORT@
CHAR@:DB 4,'CHAR'
CHAR:CALL DOCOL
 DW WORD,DROP,CFET,EXIT,CHAR@
BCHAR@:DB 6+80H,'[CHAR]'
BCHAR:CALL DOCOL
 DW TXLIT,COM,CHAR,COM,EXIT,BCHAR@
COMCF@:DB 3,',CF'
COMCF:CALL DOCOL
 DW LIT,0CDH,CCOM,COM,EXIT,COMCF@
STOCF@:DB 3,'!CF'
STOCF:CALL DOCOL
 DW LATST,FET,NTOC,INC,STO,EXIT,STOCF@
TXCRE@:DB 9,'''(CREATE)'
TXCRE:CALL DOCRE
DOCRE:POP HL
 PUSH BC
 LD B,H
 LD C,L
 JP NEXT
 DW TXCRE@
CMOVE@:DB 5,'CMOVE'
CMOVE:CALL DOCOL
CMOVE1:DW DUP,QBR,CMOVE2,TOR,OVER,CFET,OVER,CSTO,SWAP,INC,SWAP,INC,RFROM,DEC,BR,CMOVE1
CMOVE2:DW THDRP,EXIT,CMOVE@
CREAT@:DB 6,'CREATE'
CREAT:CALL DOCOL
 DW LATST,FET,COM,HERE,LATST,STO,WORD,COMCS,TXCRE,COMCF,EXIT,CREAT@
VAR@:DB 8,'VARIABLE'
VAR:CALL DOCOL
 DW CREAT,LIT,2,ALLOT,EXIT,VAR@
TXCON@:DB 11,'''(CONSTANT)'
TXCON:CALL DOCRE
DOCON:POP HL
 PUSH BC
 LD C,(HL)
 INC HL
 LD B,(HL)
 JP NEXT
 DW TXCON@
CON@:DB 8,'CONSTANT'
CON:CALL DOCOL
 DW CREAT,TXCON,STOCF,COM,EXIT,CON@
RECUR@:DB 7+80H,'RECURSE'
RECUR:CALL DOCOL
 DW LATST,FET,NTOC,COM,EXIT,RECUR@
IMMED@:DB 9,'IMMEDIATE'
IMMED:CALL DOCOL
 DW LATST,FET,DUP,CFET,LIT,80H,OR,SWAP,CSTO,EXIT,IMMED@
LBRAC@:DB 1+80H,'['
LBRAC:CALL DOCOL
 DW FALSE,STATE,STO,EXIT,LBRAC@
RBRAC@:DB 1,']'
RBRAC:CALL DOCOL
 DW TRUE,STATE,STO,EXIT,RBRAC@
HIDE@:DB 4,'HIDE'
HIDE:CALL DOCOL
 DW LATST,FET,DUP,CFET,LIT,40H,OR,SWAP,CSTO,EXIT,HIDE@
REV@:DB 6,'REVEAL'
REV:CALL DOCOL
 DW LATST,FET,DUP,CFET,LIT,3FH,AND,SWAP,CSTO,EXIT,REV@
TXCOL@:DB 3,'''(:)'
TXCOL:CALL DOCRE
DOCOL:LD (IX),E
 INC IX
 LD (IX),D
 INC IX
 POP HL
 JP NEXTHL
 DW TXCOL@
COLON@:DB 1,':'
COLON:CALL DOCOL
 DW CREAT,TXCOL,STOCF,HIDE,RBRAC,EXIT,COLON@
SMCLN@:DB 1+80H,';'
SMCLN:CALL DOCOL
 DW REV,LIT,EXIT,COM,LBRAC,EXIT,SMCLN@
LSTWRD EQU SMCLN@
ENDDCT:DB 0
